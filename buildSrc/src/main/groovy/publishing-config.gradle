plugins {
	id "java-library"
	id "signing"
	id "io.github.gradle-nexus.publish-plugin"
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// OSSRH publishing
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

String hibernatePublishUsername = project.hasProperty( 'hibernatePublishUsername' )
		? project.property( 'hibernatePublishUsername' )
		: null
String hibernatePublishPassword = project.hasProperty( 'hibernatePublishPassword' )
		? project.property( 'hibernatePublishPassword' )
		: null

publishing {
	publications {
		publishedArtifacts( MavenPublication ) {
			pom {
				name = "Hibernate Models"
				description = project.description
				url = "https://github.com/hibernate/hibernate-models"

				organization {
					name = 'Hibernate.org'
					url = 'https://hibernate.org'
				}

				licenses {
					license {
						name = "Apache License Version 2.0"
						url = "http://www.apache.org/licenses/"
						distribution = "repo"
					}
				}

				scm {
					url = "https://github.com/hibernate/hibernate-models"
					connection = "scm:git:https://github.com/hibernate/hibernate-models.git"
					developerConnection = "scm:git:https://github.com/hibernate/hibernate-models.git"
				}

				issueManagement {
					system = "github"
					url = "https://github.com/hibernate/hibernate-models"
				}

				developers {
					developer {
						id = "hibernate-team"
						name = "The Hibernate Development Team"
						organization = "Hibernate.org"
						organizationUrl = "https://hibernate.org"
					}
				}

			}

		}
	}
}

nexusPublishing {
	repositories {
		sonatype {
			username = hibernatePublishUsername
			password = hibernatePublishPassword
		}
	}
}

def isReleaseVersion = !version.endsWith("SNAPSHOT")

signing {
	sign publishing.publications.publishedArtifacts
}

tasks.withType( Sign ).configureEach {
	onlyIf { isReleaseVersion }
}

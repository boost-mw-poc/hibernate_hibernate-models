
group = "org.hibernate.models"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Version handling
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

def versionFile = rootProject.file( "version.txt" )
def versionFileVersion = extractVersionFileVersion(versionFile)
def releaseVersion = determineVersion("releaseVersion", versionFileVersion)
def developmentVersion = determineVersion("developmentVersion", versionFileVersion)

project.version = releaseVersion != null
		? releaseVersion
		: developmentVersion

ext.versionFile = versionFile
ext.releaseVersion = releaseVersion
ext.developmentVersion = developmentVersion


private static String extractVersionFileVersion(File versionFile) {
	def versionFileVersion = versionFile.text.trim()
	if ( versionFileVersion.length() <= 0 ) {
		throw new RuntimeException("Project version unknown")
	}
	return versionFileVersion
}

private String determineVersion(String type, String versionFileVersion) {
	if ( project.hasProperty( type ) ) {
		// only use the provided value if it is non-empty
		def incomingValue = project.property( type ) as String
		if ( incomingValue != null && incomingValue.trim().length() > 0 ) {
			return incomingValue
		}
	}
	return versionFileVersion
}
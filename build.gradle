plugins {
	id "java-library"
	id "checkstyle"

	id "maven-publish"
	id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
	id "base-information"
	id "publishing-config"
	id "signing-config"
	id "release-process"
}

dependencies {
	implementation libs.jandex
	implementation libs.logging

	compileOnly libs.loggingAnnotations

	annotationProcessor libs.loggingProcessor
	annotationProcessor libs.logging
	annotationProcessor libs.loggingAnnotations

	testImplementation testLibs.junit5Api
	testImplementation testLibs.assertjCore
	testImplementation platform( libs.hibernatePlatform )
	testImplementation libs.hibernateCore

	testRuntimeOnly testLibs.junit5Engine
}


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Java handling
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

java {
	sourceCompatibility = jdks.versions.baseline.get() as int
	targetCompatibility = jdks.versions.baseline.get() as int

	withJavadocJar()
	withSourcesJar()
}

test {
	useJUnitPlatform()
}

// create a single "compile" task
tasks.register( "compile" ).configure {
	dependsOn tasks.withType( JavaCompile )
}

tasks.withType( JavaCompile ).configureEach {javaCompile->
	options.encoding = "UTF-8"
	options.warnings false
}


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Javadoc
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

tasks.named( "javadoc", Javadoc ) {
	configure( options ) {
		use = true
		encoding = 'UTF-8'

		addStringOption( 'Xdoclint:none', '-quiet' )

		tags(
				'todo:X',
				'apiNote:a:API Note:',
				'implSpec:a:Implementation Specification:',
				'implNote:a:Implementation Note:',
				'settingDefault:f:Default Value:'
		)
	}
}


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Checkstyle
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

checkstyle {
	sourceSets = [ project.sourceSets.main ]
	showViolations = false
}


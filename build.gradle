plugins {
	id "java-library"
	id "checkstyle"

	id "maven-publish"
	id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
	id "publishing-pom"
	id "signing"
}

dependencies {
	implementation libs.jandex
	implementation libs.logging

	compileOnly libs.loggingAnnotations

	annotationProcessor libs.loggingProcessor
	annotationProcessor libs.logging
	annotationProcessor libs.loggingAnnotations

	testImplementation testLibs.junit5Api
	testImplementation testLibs.assertjCore
	testImplementation platform( libs.hibernatePlatform )
	testImplementation libs.hibernateCore

	testRuntimeOnly testLibs.junit5Engine
}


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Version handling
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

def versionFile = project.file( "version.txt" )
def releaseVersion = project.hasProperty('releaseVersion')
		? project.property('releaseVersion') as String
		: null
def developmentVersion = project.hasProperty('developmentVersion')
		? project.property('developmentVersion') as String
		: null

ext.versionFile = versionFile
ext.releaseVersion = releaseVersion
ext.developmentVersion = developmentVersion

group = "org.hibernate.models"
project.version = project.ext.releaseVersion != null
		? project.ext.releaseVersion
		: project.ext.versionFile.text

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Java handling
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

java {
	sourceCompatibility = jdks.versions.baseline.get()
	targetCompatibility = jdks.versions.baseline.get()
	toolchain {
		languageVersion = JavaLanguageVersion.of( jdks.versions.baseline.get() )
	}

	withJavadocJar()
	withSourcesJar()
}

test {
	useJUnitPlatform()
}

// create a single "compile" task
tasks.register( "compile" ).configure {
	dependsOn tasks.withType( JavaCompile )
}

tasks.withType( JavaCompile ).configureEach {javaCompile->
	options.encoding = "UTF-8"
	options.warnings false
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Checkstyle
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

checkstyle {
	sourceSets = [ project.sourceSets.main ]
	showViolations = false
}

